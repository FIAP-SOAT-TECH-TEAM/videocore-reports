plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.graalvm.buildtools.native' version '0.11.3'
    id 'co.uzzu.dotenv.gradle' version "4.0.0"
    id 'jacoco'
    id 'org.sonarqube' version '7.2.2.6593'
}

group = 'com.fiap.videocore'
version = '0.0.1'
description = 'Microsserviço de gerenciamento dos reportes de atualização do status de processamento de um vídeo'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

repositories {
	mavenCentral()
}

ext {
    set('lombokVersion', "1.18.42")
    set('mapStructVersion', "1.6.3")
    set('logstashEncoderVersion', "9.0")
    set('azureBlobSdkVersion', "12.31.1")

    set('openTelBomVersion', "1.58.0")
    set('openTelInstBomVersion', "2.23.0-alpha")
    set('micrometerBomVersion', "1.16.2")
    set('springAzureCloudBomVersion', "7.0.0-beta.1")
    set('springDocBomVersion', "3.0.1")
}

dependencyManagement {
    imports {
        mavenBom "io.opentelemetry:opentelemetry-bom:${openTelBomVersion}"
        mavenBom "io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom-alpha:${openTelInstBomVersion}"
        mavenBom "io.micrometer:micrometer-bom:${micrometerBomVersion}"
        mavenBom "com.azure.spring:spring-cloud-azure-dependencies:${springAzureCloudBomVersion}"
        mavenBom "org.springdoc:springdoc-openapi-bom:${springDocBomVersion}"
    }
}

dependencies {

    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'

    // Azure
    implementation 'com.azure.spring:spring-cloud-azure-starter'
    implementation 'com.azure.spring:spring-messaging-azure-servicebus'
    implementation 'com.azure.spring:spring-cloud-azure-starter-data-cosmos'
    implementation "com.azure:azure-storage-blob:${azureBlobSdkVersion}"

    // SpringDoc (Swagger/OpenAPI)
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-api"

    // AOP
    implementation 'org.springframework.boot:spring-boot-starter-aspectj'

    // Observabilidade (Instrumentação automática)
    implementation 'org.springframework.boot:spring-boot-starter-opentelemetry'
    runtimeOnly 'org.springframework.boot:spring-boot-starter-actuator'
    runtimeOnly 'io.opentelemetry.instrumentation:opentelemetry-spring-boot-starter'
    runtimeOnly "net.logstash.logback:logstash-logback-encoder:${logstashEncoderVersion}"

    // Lombok e MapStruct
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "org.mapstruct:mapstruct:${mapStructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"

    // Teste
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
    finalizedBy(jacocoTestReport)
}

sonar {
    properties {
        property 'sonar.projectKey', 'FIAP-SOAT-TECH-TEAM_videocore-reports'
        property 'sonar.organization', 'fiap-soat-tech-team'
        property 'sonar.projectName', 'videocore-reports'
        property 'sonar.token', System.getenv("SONAR_CLOUD_TOKEN") ?: env.SONAR_CLOUD_TOKEN.value
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/test/jacocoTestReport.xml'
        property 'sonar.exclusions', [
                // Application main class
                '**/ReportsApplication.java',
                // Configuration classes
                '**/config/**',
                '**/*Config.java',
                '**/*Configuration.java',
                '**/*Properties.java',
                // Exceptions
                "**/*exception*/**",
                // DTOs and Request/Response classes
                '**/dto/**',
                '**/request/**',
                '**/response/**',
                '**/payload/**',
                // MapStruct generated mappers
                '**/mapper/**/*Impl.java',
                '**/mapper/**/*MapperImpl*.java',
                // Infrastructure adapters (controllers, repositories, etc.)
                '**/infrastructure/in/**',
                '**/infrastructure/out/**',
                // Common infrastructure
                '**/infrastructure/common/**',
                // Common Observability
                '**/common/observability/**',
                // Package info files
                '**/package-info.java'
        ].join(',')
        property 'sonar.test.exclusions', '**/fixtures/**'
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    // Application main class
                    '**/ReportsApplication.java',
                    // Configuration classes
                    '**/config/**',
                    '**/*Config.java',
                    '**/*Configuration.java',
                    '**/*Properties.java',
                    // Exceptions
                    "**/*exception*/**",
                    // DTOs and Request/Response classes
                    '**/dto/**',
                    '**/request/**',
                    '**/response/**',
                    '**/payload/**',
                    // MapStruct generated mappers
                    '**/mapper/**/*Impl.java',
                    '**/mapper/**/*MapperImpl*.java',
                    // Infrastructure adapters (controllers, repositories, etc.)
                    '**/infrastructure/in/**',
                    '**/infrastructure/out/**',
                    // Common infrastructure
                    '**/infrastructure/common/**',
                    // Package info files
                    '**/package-info.java'
            ])
        }))
    }
}